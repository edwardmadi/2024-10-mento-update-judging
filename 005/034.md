Fit Menthol Sawfish

High

# `GoodDollarExpansionController::mintUBIFromReserveBalance()` does not call `Reserve::getReserveAddressesCollateralAssetBalance()`, not providing the right amount to mint

### Summary

The Reserve contract allows sending collateral to other reserves, which means that the total collateral value is fetched by doing [Reserve::getReserveAddressesCollateralAssetBalance()](https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/swap/Reserve.sol#L632).

`GoodDollarExpansionController::mintUBIFromReserveBalance()` [checks](https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L157) the collateral asset balance of the reserve by calling `balanceOf()` instead of using `Reserve::getReserveAddressesCollateralAssetBalance()`, which will return a smaller value, not reflecting the full interest earned.

As such, less $G will be minted for distribution.

### Root Cause

In `GoodDollarExpansionController:157`, the balance of the reserve is fetched with `balanceOf()` instead of `Reserve::getReserveAddressesCollateralAssetBalance()`.

### Internal pre-conditions

None.

### External pre-conditions

None.

### Attack Path

1. Anyone calls `GoodDollarExpansionController::mintUBIFromReserveBalance()` but not enough $G is minted due to the incorrect balance fetched.

### Impact

Less $G is minted than it should to be distributed to the `distributionHelper`.

### PoC

`GoodDollarExpansionController::mintUBIFromReserveBalance()` uses `balanceOf()`:
```solidity
function mintUBIFromReserveBalance(bytes32 exchangeId) external returns (uint256 amountMinted) {
  IBancorExchangeProvider.PoolExchange memory exchange = IBancorExchangeProvider(address(goodDollarExchangeProvider))
    .getPoolExchange(exchangeId);

  uint256 contractReserveBalance = IERC20(exchange.reserveAsset).balanceOf(reserve);
  uint256 additionalReserveBalance = contractReserveBalance - exchange.reserveBalance;
  if (additionalReserveBalance > 0) {
    amountMinted = goodDollarExchangeProvider.mintFromInterest(exchangeId, additionalReserveBalance);
    IGoodDollar(exchange.tokenAddress).mint(address(distributionHelper), amountMinted);

    // Ignored, because contracts only interacts with trusted contracts and tokens
    // slither-disable-next-line reentrancy-events
    emit InterestUBIMinted(exchangeId, amountMinted);
  }
}
```
But the actual balance of the reserve is given by `Reserve::getReserveAddressesCollateralAssetBalance()`:
```solidity
function getReserveAddressesCollateralAssetBalance(address collateralAsset) public view returns (uint256) {
  require(checkIsCollateralAsset(collateralAsset), "specified address is not a collateral asset");
  uint256 reserveCollateralAssetBalance = 0;
  // slither-disable-next-line cache-array-length
  for (uint256 i = 0; i < otherReserveAddresses.length; i++) {
    // slither-disable-next-line calls-loop
    reserveCollateralAssetBalance = reserveCollateralAssetBalance.add(
      IERC20(collateralAsset).balanceOf(otherReserveAddresses[i])
    );
  }
  return reserveCollateralAssetBalance.add(IERC20(collateralAsset).balanceOf(address(this)));
}
```

### Mitigation

Call `Reserve::getReserveAddressesCollateralAssetBalance()` to get the collateral balance of the reserve.