Virtual Bone Hyena

High

# `GoodDollarExpansionController.sol#mintUBIFromReserveBalance()` function uses substraction between non-scaled and scaled amounts.

### Summary

The `exchange.reserveBalance` is scaled amount. But `mintUBIFromReserveBalance()` calculates additionalReserveBalance with substraction between non-scaled balance and scaled `exchange.reserveBalance`.

https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L158

### Root Cause

We can see that `exchange.reserveBalance` is scaled amount from following code snippet.
```solidity
    File: BancorExchangeProvider.sol
    function executeSwap(bytes32 exchangeId, address tokenIn, uint256 scaledAmountIn, uint256 scaledAmountOut) internal {
      PoolExchange memory exchange = getPoolExchange(exchangeId);
      if (tokenIn == exchange.reserveAsset) {
@>      exchange.reserveBalance += scaledAmountIn;
        exchange.tokenSupply += scaledAmountOut;
      } else {
        require(exchange.reserveBalance >= scaledAmountOut, "Insufficient reserve balance for swap");
@>      exchange.reserveBalance -= scaledAmountOut;
        exchange.tokenSupply -= scaledAmountIn;
      }
      exchanges[exchangeId].reserveBalance = exchange.reserveBalance;
      exchanges[exchangeId].tokenSupply = exchange.tokenSupply;
    }
```
But `GoodDollarExpansionController.sol#mintUBIFromReserveBalance()` function is as follows.
```solidity
    function mintUBIFromReserveBalance(bytes32 exchangeId) external returns (uint256 amountMinted) {
        IBancorExchangeProvider.PoolExchange memory exchange = IBancorExchangeProvider(address(goodDollarExchangeProvider))
        .getPoolExchange(exchangeId);

157     uint256 contractReserveBalance = IERC20(exchange.reserveAsset).balanceOf(reserve);
158     uint256 additionalReserveBalance = contractReserveBalance - exchange.reserveBalance;
        if (additionalReserveBalance > 0) {
        amountMinted = goodDollarExchangeProvider.mintFromInterest(exchangeId, additionalReserveBalance);
        IGoodDollar(exchange.tokenAddress).mint(address(distributionHelper), amountMinted);

        // Ignored, because contracts only interacts with trusted contracts and tokens
        // slither-disable-next-line reentrancy-events
        emit InterestUBIMinted(exchangeId, amountMinted);
        }
    }
```
On L157, `contractReserveBalance` is non-scaled amount. But L158 substracted scaled `exchange.reserveBalance` from non-scaled contractReserveBalance.   
This is wrong.   
If `exchange.reserveAsset` has decimals less than 18, it will always be reverted.

### Internal pre-conditions

When `reserveAsset` has decimals less than 18.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

When `reserveAsset` has decimals less than 18, `GoodDollarExpansionController.sol#mintUBIFromReserveBalance()` is always reverted.

### PoC

_No response_

### Mitigation

`GoodDollarExpansionController.sol#mintUBIFromReserveBalance()` function has to be modified as follows.
```solidity
    function mintUBIFromReserveBalance(bytes32 exchangeId) external returns (uint256 amountMinted) {
        IBancorExchangeProvider.PoolExchange memory exchange = IBancorExchangeProvider(address(goodDollarExchangeProvider))
        .getPoolExchange(exchangeId);

        uint256 contractReserveBalance = IERC20(exchange.reserveAsset).balanceOf(reserve);
-       uint256 additionalReserveBalance = contractReserveBalance - exchange.reserveBalance;
+       uint256 reserveDecimals = IERC20(exchange.reserveAsset).decimals();
+       uint256 reserveMultiplier = 10 ** (18 - reserveDecimals);
+       uint256 additionalReserveBalance = (contractReserveBalance * reserveMultiplier - exchange.reserveBalance) / reserveMultiplier;
        if (additionalReserveBalance > 0) {
            amountMinted = goodDollarExchangeProvider.mintFromInterest(exchangeId, additionalReserveBalance);
            IGoodDollar(exchange.tokenAddress).mint(address(distributionHelper), amountMinted);

            // Ignored, because contracts only interacts with trusted contracts and tokens
            // slither-disable-next-line reentrancy-events
            emit InterestUBIMinted(exchangeId, amountMinted);
        }
    }
```