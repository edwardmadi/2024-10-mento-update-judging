Flaky Charcoal Elephant

High

# Precision loss in mintUBIFromExpansion() leads to lost UBI payments

### Summary

In GoodDollarExpansionController.sol, mintUBIFromExpansion() rounds the time since the last expansion down to the nearest multiple of expansionFrequency. It also sets lastExpansion to the current timestamp. Because of this, any mintUBIFromExpansion() calls that are not called exactly on a multiple of expansionFrequency will lead to lost payments. In a worst case scenario, UBI recipients will only receive 1 day's worth of payments for 1.99999 day's worth of time.

### Root Cause

The root cause of this issue is a combination how [_getReserveRatioScalar()](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L232) calculates the number of expansions and how [mintUBIFromExpansion()](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L179) keeps track of when the last expansion happened. _getReserveRatioScalar() chops off all decimals and mintUBIFromExpansion() stores the current timestamp. These means that any remainder of (block.timestamp - config.lastExpansion) / config.expansionFrequency is permanently lost.

This is in _getReserveScalar() and shows where the decimals are chopped off"
```solidity
    if (config.lastExpansion == 0) {
      numberOfExpansions = 1;
    } else {
@>    numberOfExpansions = (block.timestamp - config.lastExpansion) / config.expansionFrequency;
    }
```

This is in mintUBIFromExpansion() and shows where the current timestamp is stored:
```solidity
    bool shouldExpand = block.timestamp > config.lastExpansion + config.expansionFrequency;
    if (shouldExpand || config.lastExpansion == 0) {
      uint256 reserveRatioScalar = _getReserveRatioScalar(config);
// @audit lastExpansion set to current timestamp, discarding any thing less than a full expansionFrequency
@>    exchangeExpansionConfigs[exchangeId].lastExpansion = uint32(block.timestamp);
      amountMinted = goodDollarExchangeProvider.mintFromExpansion(exchangeId, reserveRatioScalar);
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Recipients of UBI from the contract will receive significantly less than they should depending on the timing of mintUBIFromExpansion() calls. In a worst case scenario, they will receive almost 50% less than they should have.

Because there is no direct incentive for a user to call this function, it is unlikely to be called the second a new round of UBI is available. Over time, this will lead to UBI expansions that are significantly less than they should have been.

### PoC

Please put the following two tests into the GoodDollarExpansionControllerTest_mintUBIFromExpansion contract in GoodDollarExpansionController.t.sol

The two tests show the difference in UBI payments over a period of time given optimal vs perfectly unoptimal timing.

```solidity
  // this test shows optimal UBI minting timing
  function test_precision_loss_mint_perfect_timing() public {
    // doing one initial expansion to not be first expansion
    // since on first expansion the expansion is always applied once.
    expansionController.mintUBIFromExpansion(exchangeId);

    uint256 startingTime = block.timestamp;
    uint256 distributionHelperBalanceBefore = token.balanceOf(distributionHelper);

    // mint UBI from expansion 50 cycles in a row at exactly the right time
    for (uint256 i = 0; i < 50; ++i) {
      skip(expansionFrequency + 1);
      expansionController.mintUBIFromExpansion(exchangeId);
    }

    uint256 distributionHelperBalanceAfter = token.balanceOf(distributionHelper);

    // assert that the balance after is 50 expansions
    assertEq(distributionHelperBalanceBefore + 50000000000000000000000, distributionHelperBalanceAfter);
    assertEq(block.timestamp, startingTime + (expansionFrequency + 1) * 50);

    // raw value assertions for comparision with next test
    assertEq(5.1e22, distributionHelperBalanceAfter);
    assertEq(block.timestamp, 4320051);
  }

  // this test shows unoptimal UBI minting timing
  function test_precision_loss_mint_sloppy_timing() public {
    // doing one initial expansion to not be first expansion
    // since on first expansion the expansion is always applied once.
    expansionController.mintUBIFromExpansion(exchangeId);

    uint256 startingTime = block.timestamp;
    uint256 distributionHelperBalanceBefore = token.balanceOf(distributionHelper);

    // mint UBI from expansion at exactly the wrong time until the timestamp the previous test ends
                                  // adding one additional expansionFrequency
                                  // so the test doesn't unfairly favor the first example
    while (block.timestamp < 4320051 + expansionFrequency) {
      skip(expansionFrequency);
      expansionController.mintUBIFromExpansion(exchangeId);
    }

    uint256 distributionHelperBalanceAfter = token.balanceOf(distributionHelper);

    // Despite taking 172750 more seconds than the first example
    // The UBI distributed is 52% lower 
    assertEq(distributionHelperBalanceBefore + 26000000000000000000000, distributionHelperBalanceAfter);
    assertEq(block.timestamp, 4320051 + 172750);
  }


}
```

Please note - for the sake of keeping the report to reasonable size the POC was written using the pre-existing GoodDollarExpansionController.t.sol. This causes problems in some circumstances because of the usage of vm.mockCall(). However this works exactly the same in a custom integration test file that does not depend on faking calls to the exchange provider contract. I'm happy to add this to the comments but it is a much longer test.

### Mitigation

The easiest way to fix this without adding complex intraday interest math is to set lastUpdated to lastUpdated + (numberOfExpansions * expansionFrequency). This would mean that no time is ever left unaccounted for when distributing UBI.