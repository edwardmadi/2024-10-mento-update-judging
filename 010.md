Pet Tawny Loris

High

# Incorrect price usage in `updateRatioForReward()` will result in significant drop of reserve ratio for non-18 decimals reserve tokens

### Summary

The `GoodDollarExchangeProvider::updateRatioForReward()` function supplies G$ reward, updating the reserve ratio and token supply of the exchange. 

However, due to an incorrect application of the reserve asset's decimal multiplier to the current price, the reserve ratio may be significantly reduced (or even drop to zero) if the reserve asset has non-18 decimals.

### Root Cause

In [GoodDollarExchangeProvider.sol#L198](https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/goodDollar/GoodDollarExchangeProvider.sol#L198), the current price is wrongly multiplied by the reserve asset multiplier.

```solidity
  function updateRatioForReward(bytes32 exchangeId, uint256 reward) external onlyExpansionController whenNotPaused {
    PoolExchange memory exchange = getPoolExchange(exchangeId);

@>  uint256 currentPriceScaled = currentPrice(exchangeId) * tokenPrecisionMultipliers[exchange.reserveAsset]; // @audit Problem Here!!!
    uint256 rewardScaled = reward * tokenPrecisionMultipliers[exchange.tokenAddress];

    ... ...
  }
```

### Internal pre-conditions

Reserve asset should have non-18 decimals like USDC in CELO network.

### External pre-conditions

_No response_

### Attack Path

This vulnerability gets revealed whenever anyone invokes the `updateRatioForReward()` function.

### Impact

This vulnerability will make the reserve ratio of an exchange significantly low or even 0 as demonstrated in the POC.

### PoC

Assuming USDC in CELO network, I added a new mock 6 decimals reserve token for a test purpose, and then created a new exchange with it.

Test code:
```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.18;
// solhint-disable func-name-mixedcase, var-name-mixedcase, state-visibility
// solhint-disable const-name-snakecase, max-states-count, contract-name-camelcase

import {console} from "forge-std/console.sol";

import { Test } from "forge-std/Test.sol";
import { GoodDollarExchangeProvider } from "contracts/goodDollar/GoodDollarExchangeProvider.sol";
import { ERC20 } from "openzeppelin-contracts-next/contracts/token/ERC20/ERC20.sol";

import { IReserve } from "contracts/interfaces/IReserve.sol";
import { IExchangeProvider } from "contracts/interfaces/IExchangeProvider.sol";
import { IBancorExchangeProvider } from "contracts/interfaces/IBancorExchangeProvider.sol";

import { GoodDollarExchangeProviderTest } from "../goodDollar/GoodDollarExchangeProvider.t.sol";

contract ERC20Mock is ERC20 {
  uint8 _decimals;
  constructor(string memory name_, string memory symbol_, uint8 decimals_) ERC20(name_, symbol_) {
    _decimals = decimals_;
  }

  function decimals() public view virtual override returns(uint8) {
    return _decimals;
  }
}

contract GoodDollarExchangeProviderTest_updateRatioForReward is GoodDollarExchangeProviderTest {
  IBancorExchangeProvider.PoolExchange public poolExchange3;

  GoodDollarExchangeProvider exchangeProvider;
  bytes32 exchangeId;
  uint256 reward;

  ERC20Mock public reserveTokenNew;

  function setUp() public override {
    super.setUp();
    reward = 1000 * 1e18;
    exchangeProvider = initializeGoodDollarExchangeProvider();

    reserveTokenNew = new ERC20Mock("USDC", "USDC", 6);
    
    poolExchange3 = IBancorExchangeProvider.PoolExchange({
      reserveAsset: address(reserveTokenNew),
      tokenAddress: address(token),
      tokenSupply: 7_000_000_000 * 1e18,
      reserveBalance: 200_000 * 1e18, // @audit Provide Scaled 200,000 USDCs
      reserveRatio: 1e8 * 0.28571428,
      exitContribution: 1e8 * 0.1
    });

    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isCollateralAsset.selector, address(reserveTokenNew)),
      abi.encode(true)
    );

    vm.prank(avatarAddress);
    exchangeId = exchangeProvider.createExchange(poolExchange3);
  }

  function test_updateRatioForReward_IncorrectCurrentPricePoc() public {
    uint32 expectedReserveRatio = 28571423;
    uint256 priceBefore = exchangeProvider.currentPrice(exchangeId);
    IBancorExchangeProvider.PoolExchange memory poolExchangeBefore = exchangeProvider.getPoolExchange(exchangeId);
    console.log("currentPrice Before:", priceBefore);
    console.log("reserveRatio Before:", poolExchangeBefore.reserveRatio);

    vm.prank(expansionControllerAddress);
    exchangeProvider.updateRatioForReward(exchangeId, reward);

    IBancorExchangeProvider.PoolExchange memory poolExchangeAfter = exchangeProvider.getPoolExchange(exchangeId);
    console.log("reserveRatio After:", poolExchangeAfter.reserveRatio);
  }
}
```

Output:
```log
[PASS] test_updateRatioForReward_IncorrectCurrentPricePoc() (gas: 68609)
Logs:
  currentPrice Before: 100000002000000
  reserveRatio Before: 28571428
  reserveRatio After: 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.24ms (158.60Âµs CPU time)

Ran 1 test suite in 8.09ms (2.24ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

As unnecessary `1e12` was multiplied to the price, the new reserve ratio becomes `0`.

### Mitigation

The problematic multiplication should be removed.

```diff
  function updateRatioForReward(bytes32 exchangeId, uint256 reward) external onlyExpansionController whenNotPaused {
    PoolExchange memory exchange = getPoolExchange(exchangeId);

-   uint256 currentPriceScaled = currentPrice(exchangeId) * tokenPrecisionMultipliers[exchange.reserveAsset];
+   uint256 currentPriceScaled = currentPrice(exchangeId);
    ... ...
  }
```
