Silly Mulberry Chinchilla

High

# The price of GoodDollar obtained through mintFromInterest() in the GoodDollarExchangeProvider is much cheaper than through swap.

### Summary

The price of GoodDollar obtained through mintFromInterest() in the GoodDollarExchangeProvider is much cheaper than through swap. This is unfair to regular users who purchase through swap and could be exploited by attackers to mint a large amount of GoodDollar for sale.

### Root Cause

mintFromInterest() calculates the price based on reserveBalance / tokenSupply.
https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExchangeProvider.sol#L172C1-L188C4
```javascript
  function mintFromInterest(
    bytes32 exchangeId,
    uint256 reserveInterest
  ) external onlyExpansionController whenNotPaused returns (uint256 amountToMint) {
    PoolExchange memory exchange = getPoolExchange(exchangeId);

    uint256 reserveinterestScaled = reserveInterest * tokenPrecisionMultipliers[exchange.reserveAsset];
    uint256 amountToMintScaled = unwrap(
@>>      wrap(reserveinterestScaled).mul(wrap(exchange.tokenSupply)).div(wrap(exchange.reserveBalance))
    );
    amountToMint = amountToMintScaled / tokenPrecisionMultipliers[exchange.tokenAddress];

    exchanges[exchangeId].tokenSupply += amountToMintScaled;
    exchanges[exchangeId].reserveBalance += reserveinterestScaled;

    return amountToMint;
  }
```
When swapping, the price is calculated based on an approximate curve of reserveBalance / (tokenSupply * reserveRatio).
https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/BancorExchangeProvider.sol#L145
```javascript
 function currentPrice(bytes32 exchangeId) public view returns (uint256 price) {
    // calculates: reserveBalance / (tokenSupply * reserveRatio)
    require(exchanges[exchangeId].reserveAsset != address(0), "Exchange does not exist");
    PoolExchange memory exchange = getPoolExchange(exchangeId);
    uint256 scaledReserveRatio = uint256(exchange.reserveRatio) * 1e10;
    UD60x18 denominator = wrap(exchange.tokenSupply).mul(wrap(scaledReserveRatio));
    price = unwrap(wrap(exchange.reserveBalance).div(denominator));
    return price;
  }
```

Therefore, with the same amount of reserve tokens, more GoodDollar can be obtained through mintFromInterest. If a UBI receiver sells the allocated GoodDollar on the Exchange, they will receive more reserve tokens than they would from interest, which would also lead to a significant drop in GoodDollarâ€™s value.

Furthermore, an attacker could exploit this to attack the system:

https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L137
```javascript
@>>    function mintUBIFromInterest(bytes32 exchangeId, uint256 reserveInterest) external {
    require(reserveInterest > 0, "Reserve interest must be greater than 0");
    IBancorExchangeProvider.PoolExchange memory exchange = IBancorExchangeProvider(address(goodDollarExchangeProvider))
      .getPoolExchange(exchangeId);

    uint256 amountToMint = goodDollarExchangeProvider.mintFromInterest(exchangeId, reserveInterest);

    require(IERC20(exchange.reserveAsset).transferFrom(msg.sender, reserve, reserveInterest), "Transfer failed");
    IGoodDollar(exchange.tokenAddress).mint(address(distributionHelper), amountToMint);

    // Ignored, because contracts only interacts with trusted contracts and tokens
    // slither-disable-next-line reentrancy-events
    emit InterestUBIMinted(exchangeId, amountToMint);
  }
```
An attacker could become a major member among UBI receivers and then generate GoodDollar through mintUBIFromInterest. This way, the attacker could be allocated a large portion of GoodDollar, which they could then sell on the Exchange for profit (since the price of GoodDollar on the Exchange is multiple times the price obtained through mintUBIFromInterest).

### Internal pre-conditions

_No response_

### External pre-conditions

The attacker becomes a major member among UBI receivers.

### Attack Path

1.	The attacker becomes a major member among UBI receivers.
2.	They generate GoodDollar through mintUBIFromInterest and obtain the majority of the GoodDollar produced.
3.	The attacker sells the GoodDollar on the Exchange.

### Impact

This could cause financial losses to the system.

### PoC

```javascript
contract GoodDollarExchangeProviderTest_mintFromInterest_compareSwap is GoodDollarExchangeProviderTest {
  GoodDollarExchangeProvider exchangeProvider;
  bytes32 exchangeId;
  uint256 reserveInterest;

  function setUp() public override {
    super.setUp();
    reserveInterest = 1000 * 1e18;
    exchangeProvider = initializeGoodDollarExchangeProvider();
    vm.prank(avatarAddress);
    exchangeId = exchangeProvider.createExchange(poolExchange);
  }

  function test_mintFromInterest_and_sell() public {
    uint256 interest = 1_000 * 1e18;
    // formula: amountToMint = reserveInterest * tokenSupply / reserveBalance
    // amountToMint = 1_000 * 7_000_000_000 / 200_000 = 35_000_000
    uint256 expectedAmountToMint = 35_000_000 * 1e18;
    uint256 priceBefore = exchangeProvider.currentPrice(exchangeId);

    vm.prank(expansionControllerAddress);
    uint256 amountToMint = exchangeProvider.mintFromInterest(exchangeId, interest);

    uint256 priceAfter = exchangeProvider.currentPrice(exchangeId);
    IBancorExchangeProvider.PoolExchange memory poolExchangeAfter = exchangeProvider.getPoolExchange(exchangeId);

    assertEq(amountToMint, expectedAmountToMint, "Minted amount should be correct");
    

    uint256 getReserveAmount =  exchangeProvider.getAmountOut(exchangeId, address(token), address(reserveToken), expectedAmountToMint);

    console2.log(interest);
    console2.log(getReserveAmount);

  }

}


```
Add above code in  file `test/unit/goodDollar/GoodDollarExchangeProvider.t.sol`, then run `forge test --mt test_mintFromInterest_and_sell -vv `
then will get:
```bash
Ran 1 test for test/unit/goodDollar/GoodDollarExchangeProvider.t.sol:GoodDollarExchangeProviderTest_mintFromInterest_compareSwap
[PASS] test_mintFromInterest_and_sell() (gas: 102085)
Logs:
  1000000000000000000000
  3130459209734987638326

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.49ms (1.63ms CPU time)
```
It can be seen that getReserveAmount is three times larger than the original interest.

### Mitigation

Selling reserveInterest to the Exchange pool at market price, while it may increase the current system price, ensures fairness and security within the system.