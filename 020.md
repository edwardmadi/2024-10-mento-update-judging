Flaky Charcoal Elephant

Medium

# If mintUBIFromExpansion() is not immediately called upon calling setExpansionConfig() UBI recipients will lose income

### Summary

mintUBIFromExpansion() only gives rewards for one expansionFrequency the first time it is called, regardless of how much time has passed since the expansion config was set.

### Root Cause

In [_getReserveRatioScalar()](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L229-L230), numberOfExpansions is set to 1 if lastExpansion was 0.

```solidity
@>  if (config.lastExpansion == 0) { // @audit sets numberOfExpansions to 1 regardless of how long it's been
      numberOfExpansions = 1;
    } else {
      numberOfExpansions = (block.timestamp - config.lastExpansion) / config.expansionFrequency;
    }

    uint256 stepReserveRatioScalar = MAX_WEIGHT - config.expansionRate;
@>  return unwrap(powu(wrap(stepReserveRatioScalar), numberOfExpansions));
// @audit numberOfExpansions used as while calculating ReserveRatioScalar
```

This happens regardless of how much time has passed since setting the expansion config.

Because numberOfExpansions is used to set the scalar value and said scalar value is used to determine how much to mint this impacts the accuracy of minting.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

UBI recipients will lose any and all income that should have been generated between the first call of setExpansionConfig() and the first call of mintUBIFromExpansion().

### PoC

This POC requires a new file because it depends on an the expansion controller and the exchange provider interacting. Currently only unit tests are available for the good dollar contracts. Please copy and paste the following into a new file titled GoodDollarIntegration.t.sol in tests/unit/goodDollar

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.18;
// solhint-disable func-name-mixedcase, var-name-mixedcase, state-visibility
// solhint-disable const-name-snakecase, max-states-count, contract-name-camelcase

import { Test } from "forge-std/Test.sol";
import { GoodDollarExchangeProvider } from "contracts/goodDollar/GoodDollarExchangeProvider.sol";
import { ERC20 } from "openzeppelin-contracts-next/contracts/token/ERC20/ERC20.sol";
import { ERC20Mock } from "openzeppelin-contracts-next/contracts/mocks/ERC20Mock.sol";
import { GoodDollarExpansionController } from "contracts/goodDollar/GoodDollarExpansionController.sol";

import { IReserve } from "contracts/interfaces/IReserve.sol";
import { IExchangeProvider } from "contracts/interfaces/IExchangeProvider.sol";
import { IBancorExchangeProvider } from "contracts/interfaces/IBancorExchangeProvider.sol";
import { IDistributionHelper } from "contracts/goodDollar/interfaces/IGoodProtocol.sol";

contract GoodDollarExchangeProviderTest is Test {
  /* ------- Events from IGoodDollarExchangeProvider ------- */

  event ExpansionControllerUpdated(address indexed expansionController);

  event AvatarUpdated(address indexed AVATAR);

  event ReserveRatioUpdated(bytes32 indexed exchangeId, uint32 reserveRatio);

  event ExchangeCreated(bytes32 indexed exchangeId, address indexed reserveAsset, address indexed tokenAddress);

  event ExchangeDestroyed(bytes32 indexed exchangeId, address indexed reserveAsset, address indexed tokenAddress);

  event ExitContributionSet(bytes32 indexed exchangeId, uint256 exitContribution);

  /* ------------------------------------------- */

  ERC20Mock public reserveToken;
  ERC20Mock public token;
  ERC20Mock public token2;

  address public reserveAddress;
  address public brokerAddress;
  address public avatarAddress;
  address public expansionControllerAddress;
  address public distributionAddress;

  address public user;
  bytes32 public exchangeId;

  GoodDollarExpansionController public expansionController;
  GoodDollarExchangeProvider public exchangeProvider;

  IBancorExchangeProvider.PoolExchange public poolExchange1;
  IBancorExchangeProvider.PoolExchange public poolExchange2;
  IBancorExchangeProvider.PoolExchange public poolExchange;

  uint64 expansionRate = 1e18 * 0.01;
  uint32 expansionFrequency = uint32(1 days);

  function setUp() public virtual {
    reserveToken = new ERC20Mock("cUSD", "cUSD", address(this), 1);
    token = new ERC20Mock("Good$", "G$", address(this), 1);
    token2 = new ERC20Mock("Good2$", "G2$", address(this), 1);

    reserveAddress = makeAddr("Reserve");
    brokerAddress = makeAddr("Broker");
    avatarAddress = makeAddr("Avatar");
    distributionAddress = makeAddr("distro");
    // expansionControllerAddress = makeAddr("ExpansionController");
    user = makeAddr("user");
    exchangeId = bytes32(0xba77f5c7bb3317643c6d81d1ef3f9913561741d92095f88efa402faf2cbe9124);

    // init
    expansionController = new GoodDollarExpansionController(false);
    exchangeProvider = new GoodDollarExchangeProvider(false);

    exchangeProvider.initialize(brokerAddress, reserveAddress, address(expansionController), avatarAddress);
    expansionController.initialize(address(exchangeProvider), distributionAddress, reserveAddress, avatarAddress);

    poolExchange1 = IBancorExchangeProvider.PoolExchange({
      reserveAsset: address(reserveToken),
      tokenAddress: address(token),
      tokenSupply: 300_000 * 1e18,
      reserveBalance: 60_000 * 1e18,
      reserveRatio: 0.2 * 1e8,
      exitContribution: 0.01 * 1e8
    });

    poolExchange2 = IBancorExchangeProvider.PoolExchange({
      reserveAsset: address(reserveToken),
      tokenAddress: address(token2),
      tokenSupply: 300_000 * 1e18,
      reserveBalance: 60_000 * 1e18,
      reserveRatio: 1e8 * 0.2,
      exitContribution: 1e8 * 0.01
    });

    poolExchange = IBancorExchangeProvider.PoolExchange({
      reserveAsset: address(reserveToken),
      tokenAddress: address(token),
      tokenSupply: 7_000_000_000 * 1e18,
      reserveBalance: 200_000 * 1e18,
      reserveRatio: 1e8 * 0.28571428,
      exitContribution: 1e8 * 0.1
    });

    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isStableAsset.selector, address(token)),
      abi.encode(true)
    );
    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isStableAsset.selector, address(token2)),
      abi.encode(true)
    );
    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isCollateralAsset.selector, address(reserveToken)),
      abi.encode(true)
    );

    vm.startPrank(avatarAddress);
    exchangeProvider.createExchange(poolExchange1);
    expansionController.setExpansionConfig(exchangeId, expansionRate, expansionFrequency);

    vm.mockCall(
      distributionAddress,
      abi.encodeWithSelector(IDistributionHelper(distributionAddress).onDistribution.selector),
      abi.encode(true)
    );

  }

  function test_lost_income_first_call_immediate() public {

    uint256 distributionHelperBalanceBefore = token.balanceOf(distributionAddress);

    // first call immediately
    expansionController.mintUBIFromExpansion(exchangeId);

    // wait 100 days then call again
    skip(100 days);
    expansionController.mintUBIFromExpansion(exchangeId);

    uint256 distributionHelperBalanceAfter = token.balanceOf(distributionAddress);
    
    // raw value assertions for comparision with next test
            // 5.278e23
    assertEq(527878492857280618378149, distributionHelperBalanceAfter);
    assertEq(block.timestamp, 8640001);
  }

  function test_lost_income_first_call_delayed() public {

    // expansion config is set in setUp()
    uint256 distributionHelperBalanceBefore = token.balanceOf(distributionAddress);

    // skip forward
    skip(100 days - expansionFrequency - 1);
    expansionController.mintUBIFromExpansion(exchangeId);

    // skip forward so each test ends in the same spot
    skip(expansionFrequency + 1);
    expansionController.mintUBIFromExpansion(exchangeId);

    uint256 distributionHelperBalanceAfter = token.balanceOf(distributionAddress);
    
    // raw value assertions for comparision with next test
            // 6.091e21
    assertEq(6091215182124273033363, distributionHelperBalanceAfter);
    assertEq(block.timestamp, 8640001);

    // both end at the same time but the second has ~98.9% less rewards
  }
    
}
```

### Mitigation

Set lastExpansion to block.timestamp when calling setExpansionConfig() so awards begin accruing immediately.