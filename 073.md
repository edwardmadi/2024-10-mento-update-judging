Gorgeous Fossilized Caribou

Medium

# Incorrect `reserveRatioScalar` calculation upon the change of `expansionConfig`.

### Summary

Previous `expansionConfig` is not got into consideration upon `setExpansionConfig()`, leading to the new `expansionConfig` to be used as it were set from the `lastExpansion` until now and as a result the `reserveRatioScalar` is calculated wrongly.

### Root Cause

The `expansionConfig.frequency` may have been passed from `lastExpansion` until `block.timestamp` but if `mintUBIFromExpansion` doesn't get called and admin calls `setExpansionConfig()`, then the new `expansionConfig.rate` will be applied as it was active since the `lastExpansion`. However, this is not the case since there were the old `expansionConfig.rate` that must had been applied. Let's look the implementation of `setExpansionConfig()` :
```solidity
  function setExpansionConfig(bytes32 exchangeId, uint64 expansionRate, uint32 expansionFrequency) external onlyAvatar {
    require(expansionRate < MAX_WEIGHT, "Expansion rate must be less than 100%");
    require(expansionRate > 0, "Expansion rate must be greater than 0");
    require(expansionFrequency > 0, "Expansion frequency must be greater than 0");

@>    exchangeExpansionConfigs[exchangeId].expansionRate = expansionRate;
@>    exchangeExpansionConfigs[exchangeId].expansionFrequency = expansionFrequency;

    emit ExpansionConfigSet(exchangeId, expansionRate, expansionFrequency);
  }
```
[Link to code](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L125)

As we can see, the `avatar` can change the `expansionConfig` whenever he wants. Now, let's see the `mintUBIFromExpansion` which calls the `_getReserveRatioScalar` :
```solidity
  function mintUBIFromExpansion(bytes32 exchangeId) external returns (uint256 amountMinted) {
    IBancorExchangeProvider.PoolExchange memory exchange = IBancorExchangeProvider(address(goodDollarExchangeProvider))
      .getPoolExchange(exchangeId);
    ExchangeExpansionConfig memory config = getExpansionConfig(exchangeId);

    bool shouldExpand = block.timestamp > config.lastExpansion + config.expansionFrequency;
    if (shouldExpand || config.lastExpansion == 0) {
@>      uint256 reserveRatioScalar = _getReserveRatioScalar(config);

      exchangeExpansionConfigs[exchangeId].lastExpansion = uint32(block.timestamp);
      amountMinted = goodDollarExchangeProvider.mintFromExpansion(exchangeId, reserveRatioScalar);

      IGoodDollar(exchange.tokenAddress).mint(address(distributionHelper), amountMinted);
      distributionHelper.onDistribution(amountMinted);

      // Ignored, because contracts only interacts with trusted contracts and tokens
      // slither-disable-next-line reentrancy-events
      emit ExpansionUBIMinted(exchangeId, amountMinted);
    }
  }

  function _getReserveRatioScalar(ExchangeExpansionConfig memory config) internal view returns (uint256) {
    uint256 numberOfExpansions;

    // If there was no previous expansion, we expand once.
    if (config.lastExpansion == 0) {
      numberOfExpansions = 1;
    } else {
@>      numberOfExpansions = (block.timestamp - config.lastExpansion) / config.expansionFrequency;
    }

@>    uint256 stepReserveRatioScalar = MAX_WEIGHT - config.expansionRate;
    return unwrap(powu(wrap(stepReserveRatioScalar), numberOfExpansions));
  }

```
[Linkt to code](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L225C1-L238C1)

As we can see, the `_getReserveRatioScalar()` is called from `mintUBIFromExpansion()` and calculates the `reserveRatioScalar` based on the **current** `expansionConfig`.

TL:DR; There were a time period from `lastExpansion` until `block.timestamp` that the old `expansionConfig` must have been used, but when the `expansionConfig` changed using `setExpansionConfig()`, it behaves like the new `expansionConfig` was applied from the `lastExpansion` until now.

### Internal pre-conditions

1. Admin to have called `#GoodDollarExpansionController::setExpansionConfig()` and have setted an `ExchangeExpansionConfig`.
2. Admin wanting to change `ExchangeExpansionConfig` and call `#GoodDollarExpansionController::setExpansionConfig()` with different `rate` and `frequency`.

### External pre-conditions

1. Between 1 and 2 internal pre-conditions, time passed from `lastExpansion` to be bigger than the previous `expansionConfig.frequency` but the `mintUBIFromExpansion` to not have got called so to update the `lastExpansion`.

### Attack Path

There is no attack path, just regular protocol flow with the `avatar` setting a `expansionConfig` but after some time passes he decides to set another `expansionConfig`.

### Impact

The impact of this vulnerability is that it will cause the `exchange.reserveRatio` to be decreased in **unintended rate** since the `reserveRatioScalar` will not be the one that it were supposed to be applied when the `expansionConfig` changed in `setExpansionConfig()`. It will, basically, lead to the `reserveRatio` to be changed as the current `expansionConfig` rate were applied for the whole duration from `lastExpansion` until now. This, however, will not be correct, since the previous `expansionConfig`  may should have changed the `reserveRatio` but the `mintUBIFromExpansion()` **never** got called. Now, the new `expansionConfig` set up will be applied as the previous never existed and the previous `expansionRate` will never decrease the `reserveAsset` but it must have done it since it's `frequency` had been passed.


### PoC

_No response_

### Mitigation

_No response_