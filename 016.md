Fit Menthol Sawfish

High

# `BancorFormula::power()` often favours the user due to significant rounding errors which will impact the liquidity curve and risk solvency

### Summary

[BancorFormula::power()](https://github.com/sherlock-audit/2024-10-mento-update/blob/main/mento-core/contracts/goodDollar/BancorFormula.sol#L378) produces rounding errors up to at least `1e11` as proved by fuzz tests. Additionally, often the rounding error ends up favoring the user when compared to the real formula.

### Root Cause

In `BancorFormula:378`, the `power()` approximation produces significant rounding errors which often favor the user.

### Internal pre-conditions

1. Pool conditions and amount out lead to favourable rounding errors for the user.

### External pre-conditions

None.

### Attack Path

1. User interacts with the pool with any function, `swapIn()` or `swapOut()` in any direction.

### Impact

Users get more than they should due to rounding errors, modifying the price curve and risking insolvency. Additionally, the readme specifies
> The bancor formula requires power fraction and that results in precision loss depending on where the system is. We've tested and ensured that precision tends to favour the protocol

Which was shown by the fuzz tests to often favour the user.

> Same as the above, there are acceptable approximations due to power precision. We've tested to ensure that we're talking about ~1e6 precision on most cases and 1e3 loss on extreme scenarios.

The errors reach 1e11 or more, significantly more than the described.

### PoC

To understand the rounding errors involved, the `BancorFormula::purchaseTargetAmount()` result was compared to the real formula, run in a python script and the absolute and relative errors were analyzed. Running `forge test --mt testSingleRun -vvvv --ffi` shows that according to the real formula the user should get 4.436e11, but gets 4.606e11 instead, getting an extra 1.7e10 from the error. This is extremely dangerous for price curves because they should always favour the protocol.

The following test file contains the tests:
```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.18;

import { BancorFormula } from "../../contracts/goodDollar/BancorFormula.sol";
import { Test, console2 } from "forge-std/Test.sol";

contract BancorFormulaFuzzTest is Test, BancorFormula {
    function setUp() public {
        vm.writeFile("fuzz_purchaseTargetAmount.xls", string.concat(
            "supply,",
            "reserveBalance,",
            "reserveRatio,",
            "amount,",
            "solidityResult,",
            "pythonResult,",
            "real - expected,",
            "relativeErr 1e18 precision",
            "\n"
        ));
    }
    function testFuzz_purchaseTargetAmount(uint256 _supply, uint256 _reserveBalance, uint32 _reserveRatio, uint256 _amount) public returns(uint256) {
        (_supply, _reserveBalance, _reserveRatio, _amount) = _boundInputs(_supply, _reserveBalance, _reserveRatio, _amount);
        uint256 tokens = purchaseTargetAmount(_supply, _reserveBalance, _reserveRatio, _amount);
        uint256 pythonResult = _pyPurchaseTargetAmount(_supply, _reserveBalance, _reserveRatio, _amount);

        (int256 absErr, uint256 relativeErr) = _getError(tokens, pythonResult);
        vm.writeLine("fuzz_purchaseTargetAmount.xls", string.concat(
            vm.toString(_supply), ",",
            vm.toString(_reserveBalance), ",",
            vm.toString(_reserveRatio), ",",
            vm.toString(_amount), ",",
            vm.toString(tokens), ",",
            vm.toString(pythonResult), ",",
            vm.toString(absErr), ",",
            vm.toString(relativeErr)
        ));

        return tokens;
    }

    function testSingleRun() public {
        testFuzz_purchaseTargetAmount(2.22e26, 8.62e26, 9.08e7, 1.97e12);
    }

    function _pyPurchaseTargetAmount(uint256 _supply, uint256 _reserveBalance, uint32 _reserveRatio, uint256 _amount) public returns (uint256) {
        return _runPython("purchaseTargetAmount", _supply, _reserveBalance, _reserveRatio, _amount);
    }

    function _runPython(string memory method, uint256 supply, uint256 reserveBalance, uint32 reserveRatio, uint256 amount) public returns (uint256) {
        string memory data =
            string.concat(
                "{\n", 
                    "\"method\": \"", method, "\",\n",
                    "\"supply\": \"", vm.toString(supply), "\",\n",
                    "\"reserveBalance\": \"", vm.toString(reserveBalance), "\",\n",
                    "\"reserveRatio\": \"", vm.toString(reserveRatio), "\",\n",
                    "\"amount\": \"", vm.toString(amount), "\"\n"
                "}"
            );
        string[] memory args = new string[](3);
        args[0] = "python3";
        args[1] = "BancorFormula.py";
        args[2] = data;
        return uint256(vm.parseBytes32(vm.toString(vm.ffi(args))));
    }

    function _getError(uint256 real, uint256 expected) internal pure returns(int256 diffInt, uint256 relativeErr) {
        if (expected == 0) {
            return (int256(real), 1e18);
        }
        uint256 diff = real > expected ? real - expected : expected - real;
        relativeErr = diff * 1e18 / expected;
        diffInt = int256(real) - int256(expected);
    }

    function _boundInputs(uint256 _supply, uint256 _reserveBalance, uint32 _reserveRatio, uint256 _amount) internal pure returns(uint256, uint256, uint32, uint256) {
        _supply = _supply % 1e27;
        _supply = bound(_supply, 1e18, 1e27);
        _reserveBalance = _reserveBalance % 1e27;
        _reserveBalance = bound(_reserveBalance, 1e18, 1e27);
        _reserveRatio = _reserveRatio % 1e8;
        _reserveRatio = uint32(bound(uint256(_reserveRatio), 0.1e8, 1e8));
        _amount = _amount % (_reserveBalance / 100);
        _amount = bound(_amount, 1, _reserveBalance);
        return (_supply, _reserveBalance, _reserveRatio, _amount);
    }
}
```
And the python file:
```python3
import json
import sys

# Read JSON string from the command line argument
json_input = sys.argv[1]

# Parse JSON string to a dictionary
data = json.loads(json_input)

# Extract values and convert them to integers
SUPPLY = int(data["supply"])
RESERVE_BALANCE = int(data["reserveBalance"])
RESERVE_WEIGHT = int(data["reserveRatio"])
MAX_WEIGHT = 1e8  # Assuming MAX_WEIGHT is a known constant
amounts = int(data["amount"])

# Calculate the result
result = SUPPLY * ((1 + amounts / RESERVE_BALANCE) ** (RESERVE_WEIGHT / MAX_WEIGHT) - 1)

# Ensure result is an integer, format it as a full integer without scientific notation
decimals_result = f"{int(result):.0f}"

# Convert the integer result to a hexadecimal string with 0x prefix
hex_result = hex(int(decimals_result))

# Ensure it's in bytes32 format by padding to 64 characters after the "0x"
hex_result = "0x" + hex_result[2:].zfill(64)

# Print the result
print(hex_result)
```

Additionally, `fs_permissions = [{ access = "read-write", path = "BancorFormula.py" },{ access = "read-write", path = "fuzz_purchaseTargetAmount.xls" }]` must be added to `foundry.toml`.

### Mitigation

The functions should ensure that the protocol is always favoured, but this is not easy to achieve with the current `power()` function.